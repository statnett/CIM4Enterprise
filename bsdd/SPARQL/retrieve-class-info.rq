PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX uml: <http://iec.ch/TC57/NonStandard/UML#>
PREFIX cims: <http://iec.ch/TC57/1999/rdf-schema-extensions-19990926#>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX onto: <http://www.ontotext.com/>
PREFIX cim: <https://cim.ucaiug.io/ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX : <:>

CONSTRUCT {
    ?class :Code ?cCode .
    ?class :Name ?cName .
    ?class :ClassType "Class" .
    ?class :Definition ?cDef .
    ?class :ParentClassCode ?parentClassCode .
    ?class :CreatorLanguageIsoCode "EN" .
    ?class :OwnedUri ?class .
    ?class :Status "Active" .
    ?class :ClassProperties _:p .
    _:p :Code ?pCode .
    _:p :PropertyUri ?prop .
    _:p :Description ?pDesc .
    _:p :Unit ?pUnit .
    _:p :OwnedUri ?propOuri .
    _:p :PropertyType "Property" .
    _:p :AllowedValues _:pv .
    _:pv :Code ?pvCode .
    _:pv :Value ?pvLabel .
    _:pv :Description ?pvDef .
    _:pv :OwnedUri ?pval .
# To be enabled if needed:
#    ?class :ClassRelations _:r .
#    _:r :RelationType ?classRel .
#    _:r :RelatedClassUri ?rel .
#    _:r :RelatedClassName ?classRelLab .
#    _:r :OwnedUri ?rel .
} WHERE {
    GRAPH onto:explicit {
        {
            values ?sclass {cim:AssetInfo}
            ?sclass a owl:Class .
            ?sclass rdfs:subClassOf* ?class .
        } UNION {
            values ?pclass {cim:AssetInfo cim:ProductAssetModel cim:Manufacturer cim:Asset cim:AssetOrganisationRole}
            ?pclass a owl:Class .
            ?class rdfs:subClassOf*  ?pclass .
        }
        BIND(REPLACE(STR(?class), "https://cim.ucaiug.io/rules#|https://cim.ucaiug.io/ns#|https://cim.ucaiug.io/ns/eu#", "") as ?cCode) .
        OPTIONAL {?class rdfs:label ?cName} .
        OPTIONAL {?class rdfs:comment ?cDef} .
        OPTIONAL {?class rdfs:subClassOf ?parentClass
                  BIND(REPLACE(STR(?parentClass), "https://cim.ucaiug.io/rules#|https://cim.ucaiug.io/ns#|https://cim.ucaiug.io/ns/eu#", "") as ?parentClassCode) .}
        OPTIONAL {
            ?rel rdfs:subClassOf|^rdfs:subClassOf* ?class .
            ?rel rdfs:label ?classRelLab
            BIND(IF(EXISTS {?rel rdfs:subClassOf ?class}, "IsChildOf", "IsParentOf") as ?classRel) .
            BIND(CONCAT(STR(?class), ?classRel, REPLACE(STR(?rel), "https://cim.ucaiug.io/rules#|https://cim.ucaiug.io/ns#|https://cim.ucaiug.io/ns/eu#", "")) as ?relUri)
            FILTER(?class != ?rel)
        }
        OPTIONAL {
            { ?prop rdfs:domain/^rdfs:subClassOf* ?class ;
                  a owl:DatatypeProperty .
              BIND(CONCAT(STR(?prop), ?cCode) as ?propOuri)
              BIND(REPLACE(?propOuri, "https://cim.ucaiug.io/rules#|https://cim.ucaiug.io/ns#|https://cim.ucaiug.io/ns/eu#", "") as ?pCode) .
              OPTIONAL {?prop rdfs:comment ?pDesc}  .
              OPTIONAL {?prop qudt:hasUnit ?pUnit}
            }
            UNION
            { ?prop rdfs:domain/^rdfs:subClassOf* ?class ;
                  a owl:ObjectProperty ;
                  rdfs:range ?range .
              BIND(CONCAT(STR(?prop), ?cCode) as ?propOuri)
              BIND(REPLACE(?propOuri, "https://cim.ucaiug.io/rules#|https://cim.ucaiug.io/ns#|https://cim.ucaiug.io/ns/eu#", "") as ?pCode) .
              ?range cims:stereotype uml:enumeration .
              OPTIONAL {?prop rdfs:comment ?pDesc}  .
              OPTIONAL {
                  ?pval rdf:type ?range ;
                      rdfs:label ?pvLabel ;
                      rdfs:comment ?pvDef .
                  BIND(REPLACE(STR(?pval), "https://cim.ucaiug.io/rules#|https://cim.ucaiug.io/ns#|https://cim.ucaiug.io/ns/eu#", "") as ?pvCode) .
              }
            }
        }
    }
}

